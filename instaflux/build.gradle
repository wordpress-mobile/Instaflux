plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "org.wordpress.android.fluxc.instaflux"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warning 'InvalidPackage'
    }
}

android.buildTypes.all { buildType ->
    // Add properties named "wp.xxx" to our BuildConfig
    Properties apiProperties = loadPropertiesOrUseExampleProperties("api.properties",
            "example app can't access WordPress.com servers")

    apiProperties.any { property ->
        if (property.key.toLowerCase().startsWith("wp.")) {
            buildType.buildConfigField "String", property.key.replace("wp.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
    }
}

def loadPropertiesOrUseExampleProperties(fileName, warningDetail) {
    Properties properties = new Properties()
    File propertiesFile = file(propertiesFilePath(fileName))
    if (propertiesFile.exists()) {
        properties.load(new InputStreamReader(new FileInputStream(propertiesFile), "utf-8"))
    } else {
        def examplePropertiesFilePath = examplePropertiesFilePath(fileName)
        logger.quiet("WARNING: you're using the '$examplePropertiesFilePath' file - $warningDetail")
        properties.load(new InputStreamReader(new FileInputStream(file(examplePropertiesFilePath)), "utf-8"))
    }
    return properties
}

static def propertiesFilePath(fileName) {
    return "../example/properties/$fileName"
}

static def examplePropertiesFilePath(fileName) {
    return "../example/properties-example/$fileName"
}

dependencies {
    implementation("org.wordpress:fluxc:develop-e490ac0557848d32e28781e7f8fca4923e587a98")

    // WordPress libs
    implementation("org.wordpress:utils:$wordPressUtilsVersion") {
        // Using official volley package
        exclude group: "com.mcxiaoke.volley"
        exclude group: "com.android.support"
    }

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Debug dependencies
    debugImplementation 'com.facebook.flipper:flipper:0.51.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.51.0'
}
