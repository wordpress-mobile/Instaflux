buildscript {
    ext.kotlinVersion = '1.4.10'
    ext.kotlinCoroutinesVersion = '1.3.9'
}

plugins {
    id "com.automattic.android.fetchstyle" version "1.1"
    id "com.automattic.android.configure" version "0.5.0"
}

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        google()
        jcenter()
        maven { url "http://dl.bintray.com/terl/lazysodium-maven" }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.31.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

ext {
    daggerVersion = '2.29.1'
    wellSqlVersion = '1.6.0'
    supportLibraryVersion = '27.1.1'
    arch_paging_version = '1.0.1'
    arch_lifecycle_version = '1.1.1'
    arch_core_version = '2.0.1'
    appcompat_version = '1.0.2'
    mockitoVersion = '3.3.3'
    assertJVersion = '3.19.0'
}
