buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta6'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    jcenter()
}

android {
    publishNonDefault true

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        versionCode 4
        versionName "0.1"
        minSdkVersion 15
        targetSdkVersion 23
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    // Support libs
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'

    // WordPress libs
    compile ('org.wordpress:utils:1.8.0') {
        // Using official volley package
        exclude group: "com.mcxiaoke.volley";
    }

    // Custom WellSql version
    compile 'org.wordpress:wellsql:1.0.6'
    apt 'org.wordpress:wellsql-processor:1.0.6'

    // External libs
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.6.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

signing {
    required {
        project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

version android.defaultConfig.versionName
group = "org.wordpress"
archivesBaseName = "stores"

// http://central.sonatype.org/pages/gradle.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'WordPress-Android-Stores'
                packaging 'aar'
                description 'A reusable Android Stores for WordPress data'
                url 'https://github.com/wordpress-mobile/WordPress-Stores-Android'
                scm {
                    connection 'scm:git:https://github.com/wordpress-mobile/WordPress-Stores-Android.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/WordPress-Stores-Android.git'
                    url 'https://github.com/wordpress-mobile/WordPress-Stores-Android'
                }

                licenses {
                    license {
                        name 'GPL v2'
                        url 'http://opensource.org/licenses/GPL-2.0'
                    }
                }

                developers {
                    developer {
                        id 'maxme'
                        name 'Maxime Biais'
                        email 'maxime@automattic.com'
                    }
                }
            }
        }
    }
}
